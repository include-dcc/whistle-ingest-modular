// Definition: Create an Observation to represent a value from the dataset such as a Condition and it's T/F Status
//
// Arguments:
//  study - This is the full study object created by Whistler
//  subject_id - This is the participant ID which will be used to build a proper reference to the Patient resource
//  tablename - The table where the variable's data was originally captured
//  varname - the name of the variable being represented
//  value - The value (of varname) being recorded
//  
// Notes:
//  
def VariableObservation(study, subject, tablename, varname, value) {
    // Inputting this system directly defeats the purpose of using a function but it is only being used for one
    // field for now (Down Syndrome Status) ALH 2025-03-10
    var coding: Harmonize(varname, "https://includedcc.org/fhir/CodeSystem/data-dictionary/participant/down_syndrome_status");

    // Is this valid to rely solely on the study for this code for these resources?
    var study_code: $StrSplit(subject.study_code, "|");
    meta.tag[]: StudyMetaMultiple(study_code[0], study.dd-prefix, "Patient");

    var external_ids: $StrSplit(subject.participant_external_id, "|");
    var study_codes: $StrSplit(subject.study_code, "|");
    identifier[]: Key_Identifier(study, "Observation", BuildObservationId(study, subject.participant_global_id, tablename, $StrCat(varname, "-", $StrJoin(".", $StrSplit(value, " ")))));
    identifier[0].use: "official";
    identifier[]: Key_Identifier_From_Code(study.dd-prefix, study_codes[], "Patient", external_ids[], "secondary");

    subject: Reference_Global_ID("Patient", subject.participant_global_id);
    // This is technically how you are expected to link to ODs, but it isn't working in the current version of FHIR
    // basedOn[].identifier: Key_Identifier(study, "ObservationDefinition", DdVariableId(study.id, tablename, varname));
    code.text: HarmonizedLocalDisplay(varname, "https://includedcc.org/fhir/CodeSystem/data-dictionary/participant/down_syndrome_status");
    code.coding[]: coding;
    
    valueCodeableConcept.text: value;
    valueCodeableConcept.coding[]: HarmonizeMapped(varname, tablename);
    status: "final";
    resourceType: "Observation";
}


